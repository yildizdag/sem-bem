!!------------------------------------------------------------
!! APDL CODE
!! CANTILEVER BEAM â€“ FREE VIBRATION ANALYSIS
!!
!! Author: M. Erden Yildizdag
!! Affiliation: Istanbul Technical University
!!
!! Description:
!! This script performs a modal (free vibration) analysis
!! of a cantilever beam using ANSYS APDL.
!!
!! Usage: For academic and research purposes only.
!!------------------------------------------------------------
/PREP7
L = 1.2
W = 0.5
H = 0.03
Y = 200E9
NU = 0.3
RHO = 2500
NELEM = 100
NUMMODE = 24
!!----------------------
!! MATERIAL PROPERTIES
!!----------------------
ET,1,BEAM188  
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,Y  
MPDATA,PRXY,1,,NU
MPDATA,DENS,1,,RHO
!!----------
!! SECTION
!!---------- 
SECTYPE,1,BEAM,RECT,,0   
SECOFFSET,CENT 
SECDATA,H,W,0,0,0,0,0,0,0,0,0,0
!!----------------------
!! KEYPOINTS AND LINES
!!----------------------
K,1,0,0,0
K,2,L,0,0
L,1,2
!!-------
!! MESH
!!-------
LESIZE,ALL,,,NELEM,,1,,,1,
LMESH,ALL
!!----------------------
!! BOUNDARY CONDITIONS
!!----------------------
NSEL,S,LOC,X,0,0 !-SELECT NODE AT LEFT END
D,ALL,,0,,,,ALL,,,,,
ALLSEL,ALL
FINISH  
/SOLU
ANTYPE,2
MODOPT,LANB,NUMMODE   
EQSLV,SPAR  
MXPAND,0, , ,0  
LUMPM,0 
PSTRES,0
MODOPT,LANB,NUMMODE,0,0,,OFF 
SOLVE   
FINISH